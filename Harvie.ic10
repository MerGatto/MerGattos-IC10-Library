define HarvieHash HASH("StructureHarvie")
define HarvieHash2 HASH("StructureHarvieReversed")
define HydroPonicHash HASH("StructureHydroponicsTrayData")

alias vHarvieCount r15
alias vHarvieCount2 r14

Main:
yield
#check planting
# check if this works when device is not there (min still 0?)
jal CountHarvies
move r0 1 # happy default
move r1 1 # happy default
breqz vHarvieCount 2
lb r0 HarvieHash Mode Minimum
breqz vHarvieCount2 2
lb r1 HarvieHash2 Mode Minimum
min r0 r1 r0
beqz r0 DoPlant

#check harvesting
#check seeding stage
lbs r0 HydroPonicHash 0 Growth Minimum
beq r0 5 DoHarvest

#check removed seeds
sge r0 r0 4 # 4 == mature
lbs r1 HydroPonicHash 0 Seeding Minimum
sge r1 r1 0 # might need correcting 0 == seeding stage but no seed
and r0 r0 r1
bnez r0 DoHarvest
sb HarvieHash On 0
sb HarvieHash2 On 0
j Main


DoPlant:
sb HarvieHash On 1
sb HarvieHash2 On 1
yield
sb HarvieHash Plant 1
sb HarvieHash2 Plant 1
j Working

DoHarvest:
sb HarvieHash On 1
sb HarvieHash2 On 1
yield
sb HarvieHash Harvest 1
sb HarvieHash2 Harvest 1
j Working

Working:
yield
jal CountHarvies
move r0 -1
move r1 -1
breqz vHarvieCount 2
lb r0 HarvieHash Activate Maximum
breqz vHarvieCount2 2
lb r1 HarvieHash2 Activate Maximum
max r0 r0 r1
bge r0 1 Working
j Main

CountHarvies:
lb r0 HarvieHash PrefabHash Sum
div vHarvieCount r0 HarvieHash
lb r0 HarvieHash2 PrefabHash Sum
div vHarvieCount2 r0 HarvieHash2
j ra
